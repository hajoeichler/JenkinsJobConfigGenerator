grammar de.hajoeichler.jenkins.JobConfig with org.eclipse.xtext.common.Terminals

generate jobConfig "http://www.hajoeichler.de/jenkins/JobConfig"

Model:
	(imports+=Import)*
	(declSpecs+=DeclSpecs)?
	(group=Group
	|
	config=Config);

Import:
	'import' importedNamespace=FqnWithWildCard;

FqnWithWildCard:
	ID ('.*')?;

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_' | '-') ('a'..'z' | 'A'..'Z' | '_' | '-' | '0'..'9')*;

BracketOpen:
	"{";

BracketClose:
	"}";

DeclSpecs:
	"declarations" BracketOpen
	decls+=(AntDecl | MavenDecl | LockDecl | MSBuildDecl | WarningsDecl | MatrixDecl)+
	BracketClose;

AntDecl:
	"ant" name=ID;

MavenDecl:
	"maven" name=ID;

LockDecl:
	"lock" name=ID;

MSBuildDecl:
	"msBuild" name=ID;

WarningsDecl:
	"warnings" name=ID;

MatrixDecl:
	"matrix" name=ID BracketOpen
	(runSequentially?="runSequentially")?
	axes+=AxisDecl+
	BracketClose;

AxisDecl:
	"axis" ("label" label=STRING
	"values" values+=STRING+);

Group:
	"group" name=ID BracketOpen
	(configs+=Config)*
	BracketClose;

Config:
	(abstract?="abstract")? "job" name=ID ("extends" parentConfig=[Config])? BracketOpen
	(disabled?="isDisabled")?
	(oldBuildHandling=OldBuildHandling)?
	(executeConcurrent?="executeConcurrent")?
	("description" description=STRING)?
	("displayName" displayName=STRING)?
	(matrixes+=Matrix)*
	("restrictRunningTo" restrictTo=STRING)?
	('gitUrl' gitUrl=STRING)?
	(concurrentBuild?="concurrentBuild")?
	(paramSection=ParameterSection)?
	(scm=Scm)?
	(trigger=TriggerSection)?
	(wrapper=WrapperSection)?
	(buildSection=BuildSection)?
	(publisherSection=PublisherSection)?
	BracketClose;

Matrix:
	"matrix" matrix=[MatrixDecl];

OldBuildHandling:
	{OldBuildHandling}
	"oldBuilds" BracketOpen
	("daysToKeep" daysToKeep=INT)?
	("maxNumberOfBuilds" maxNumberOfBuilds=INT)?
	("daysToKeepArtifact" daysToKeepArtifact=INT)?
	("maxNumberOfBuildsWithArtifact" maxNumberOfBuildsWithArtifact=INT)?
	BracketClose;

ParameterSection:
	"params" BracketOpen
	parameters+=Parameter+
	BracketClose;

Parameter:
	name=STRING BracketOpen
	type=ParameterType
	("description" description=STRING)?
	BracketClose;

ParameterType:
	StringParam | BooleanParam | ChoiceParam;

StringParam:
	"String" value=STRING;

BooleanParam:
	"Boolean" ((checked?="trueByDefault") | (notChecked?="falseByDefault"));

ChoiceParam:
	"Choice" choices=STRING;

Scm:
	ScmGit | ScmSVN | ScmCVS;

ScmGit:
	"git" BracketOpen "url" url=STRING ("branch" branch=STRING)? (wipeOutWorkspace?="wipeOutWorkspace")?
	("excludedRegions" excludedRegions=STRING)? ("includedRegions" includedRegions=STRING)? BracketClose;

ScmSVN:
	"svn" BracketOpen "url" url=STRING ("localDir" localDir=STRING)? ("strategy" strategy=STRING) ("browser"
	browser=STRING)? BracketClose;

ScmCVS:
	"cvs" BracketOpen "cvsroot" root=STRING "modules" modules=STRING (useUpdate?="useUpdate")? BracketClose;

TriggerSection:
	"trigger" BracketOpen buildtriggers+=(TimerTrigger | PollScmTrigger | FirstStartTrigger | GitHubPushTrigger)+
	BracketClose;

TimerTrigger:
	"periodically" timer=STRING;

PollScmTrigger:
	"pollScm" poll=STRING;

FirstStartTrigger:
	buildOnFirstStart?="buildOnFirstStart";

GitHubPushTrigger:
	buildOnPushToGitHub?="buildOnPushToGitHub";

WrapperSection:
	"wrappers" BracketOpen
	wrappers+=(AnsiColor | Release | Timeout | Lock | ExclusiveExecution | MatrixTieParent)+
	BracketClose;

AnsiColor:
	(ansicolor?='ansicolor');

Release:
	{Release}
	"release" BracketOpen
	(notKeepForever?='notKeepForever')?
	(paramSection=ParameterSection)?
	("pre" preBuildSection=BuildSection)?
	("success" successBuildSection=BuildSection)?
	("failed" failedBuildSection=BuildSection)?
	("final" finalBuildSection=BuildSection)?
	BracketClose;

Timeout:
	"timeout" t=INT (failBuild?='failBuild')?;

Lock:
	"lock" lock=[LockDecl];

ExclusiveExecution:
	(exclusiveExecution?='exclusiveExecution');

MatrixTieParent:
	"tie matrix parent to" matrixParent=STRING;

BuildSection:
	"builds" BracketOpen
	builds+=(Maven | Ant | Shell | Batch | SystemGroovy | MSBuild | TriggerBuilderSection)+
	BracketClose;

Maven:
	"maven" version=[MavenDecl] BracketOpen
	"goals" mavenGoals=STRING
	("pom" mavenPOM=STRING)?
	("properties" mavenProperties=STRING)?
	(mavenPrivateRepo?="usePrivateRepo")?
	BracketClose;

Ant:
	"ant" version=[AntDecl] BracketOpen
	("buildFile" buildFile=STRING)?
	("targets" targets=STRING)?
	BracketClose;

Shell:
	"sh" BracketOpen
	shellScript=STRING
	(append?="append")?
	BracketClose;

Batch:
	"batch" BracketOpen
	batchScript=STRING
	(append?="append")?
	BracketClose;

SystemGroovy:
	"systemGroovy" BracketOpen
	groovyScript=STRING
	// TODO support more config
	BracketClose;

MSBuild:
	"msbuild" version=[MSBuildDecl] BracketOpen
	"buildFile" buildFile=STRING
	("args" targets=STRING)?
	BracketClose;

TriggerBuilderSection:
	"triggered builds" BracketOpen
	(triggeredBuilds+=TriggeredBuild)+
	BracketClose;

TriggeredBuild:
	builds=[Config]
	(blocking?='blocking')?
	(triggerParams+=(CurrentTriggerParams | PropertyFileTriggerParams | PredefinedTriggerParams)+)?;

CurrentTriggerParams:
	currentParams?='currentParams';

PropertyFileTriggerParams:
	'propertyFile' propertyFile=STRING;

PredefinedTriggerParams:
	'predefined' predefined=STRING;

PublisherSection:
	"publishers" BracketOpen
	publishers+=(Artifacts | TestResult | ExtMail | DownStream | HTMLPublisher | Warnings | Cobertura | Claim | Rcov |
	FindBugs | HipChat | PlayAutoTestReport | Violations)+
	BracketClose;

Artifacts:
	"artifacts" artifacts=STRING (onlyLatest?="onlyLatest")?;

TestResult:
	"testresults" testresults=STRING (longIO?="longIO")? (claim?="claim")?;

	// TODO: support multiple
HTMLPublisher:
	"htmlpublisher" name=STRING "dir" dir=STRING "files" files=STRING (keepPast?='keepPast')?;

Violations:
	"violations" BracketOpen
	violations+=ViolationsConfig
	BracketClose;

ViolationsConfig:
	type=("checkstyle" | "csslint" | "findbugs" | "jslint") "min" min=INT "max" max=INT "unstable" unstable=INT "pattern"
	pattern=STRING;

Warnings: // TODO: move treshholds etc. into own rule
	"warnings" parser=[WarningsDecl] ("unstableTotalAll" unstableTotalAll=INT)? ("failTotalAll" failTotalAll=INT)?
	(computeNewWarnings?="computeNewWarnings")?;

Cobertura:
	"cobertura" xmlreport=STRING (onlyStable?="onlyStable")?;

Rcov:
	"rcov" reportDir=STRING;

FindBugs:
	"findbugs" results=STRING;

Claim:
	(canClaim?="canClaim");

HipChat:
	{HipChat} "hipchat" ("room" room=STRING)?;

PlayAutoTestReport:
	(reportPlayAutoTest?="reportPlay!auto-test");

ExtMail:
	{ExtMail} "extMail" (mergeWithSuperConfig?="mergeWithSuperConfig")? BracketOpen ("to" to=STRING)? ("type" type=("html"
	| "text"))? ("subject" subject=STRING)? ("content" content=STRING)? ("attachments" attachments=STRING)? ("trigger"
	BracketOpen (mailTrigger+=MailTrigger)+ BracketClose)? BracketClose;

MailTrigger:
// TODO: check new types
	type=("Still-Failing" | "Success" | "Fixed" | "Still-Unstable" | "Before-Build" | "Failure" | "Unstable")
	(toList?="toList")? (toCommiter?="toCommiter")? (toRequester?="toRequester")? (toCulprits?="toCulprits")? ("to"
	to=STRING)? ("subject" subject=STRING)? ("content" content=STRING)?;

DownStream:
	{DownStream} "downstream" BracketOpen builds+=DownStreamBuild* BracketClose;

DownStreamBuild:
	builds=[Config] "when" condition=("Stable" | "Unstable" | "Not-Failed" | "Failed" | "Complete")
	triggerParams+=(CurrentTriggerParams | PropertyFileTriggerParams | PredefinedTriggerParams | GitCommitParam)*;

GitCommitParam:
	passThroughGitCommit?="passThroughGitCommit";